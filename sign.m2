{*
Description: Main method of the file is signSpace, which finds the
non-zero sign vectors of a given vector subspace. 

Authors: 
Jacob Zoromski (jzoromski@wisc.edu)


Packages used: Polyhedra

Methods:

1) sgn
Description: Applies the sign function to each component of a vector
Input: v, a column vector (as a matrix or element of a free module)
Output: signVector, a list

2) signMatrices
Description:  Lists all diagonal nxn matrices with entries in {0,1,-1}
Input: n, an integer
Output: listMatrices, a list of matrices

3) signSpace
Description: Computes all non-zero sign vectors of a linear subspace given 
by the span of the columns of a matrix M
Input: M, a matrix
Output: signs, a list of sign vectors (as lists)

Example:
M = transpose matrix {{{1,1,0,-1},{0,2,1,-1}}
transpose matrix signSpace(M) = 
| -1 -1 -1 -1 -1 -1 -1 1  1  0  1  0  1  1  1  1  |
| -1 -1 -1 1  1  0  1  -1 -1 -1 0  1  1  1  -1 1  |
| -1 0  1  1  1  1  1  -1 -1 -1 -1 1  -1 0  -1 1  |
| 1  1  1  -1 0  1  1  -1 0  1  -1 -1 -1 -1 1  -1 |
*}


sgn = (v) -> (
    V := matrix v;
    signVector := new List;
    for i from 0 to numRows(V) - 1 do (
	if V_(i,0) > 0 then signVector = append(signVector, 1)
	else if V_(i,0) < 0 then signVector = append(signVector, -1)
	else signVector = append(signVector,0)
	);
    signVector
    )


signMatrices = (n) -> (
    minus1 := toList(1..n);
    minus1 = apply(minus1, i -> -1);
    plus1 := apply(minus1, i -> 1);
    listMatrices := toList(minus1..plus1);
    listMatrices  = apply(listMatrices, v -> diagonalMatrix v );
    listMatrices
    )


signSpace = (M) -> (
    signVectors := new List;
    listCones := new List;
    listRays := new List;
    interiorVectors := new List;
    listSignMatrices := signMatrices(numRows M);
    --Makes cones generated by M and all copies of M with rows 
    --multiplied by 1,-1, or 0
    listCones = apply(listSignMatrices, v -> (v * M));
    listCones = apply(listCones, A -> coneFromHData(A));
    listCones = unique(listCones);
    --Makes list of non-zero rays that generate the cones
    listRays = apply(listCones, i -> rays(i));
    listRays = apply(listRays, i -> if zero i then 0 else i);
    listRays = delete(0, listRays);
    --Finds interior vector of each cone by adding its rays
    interiorVectors = apply(listRays, C -> (
	    S := C_0;
	    for j from 1 to numColumns C - 1 do (
	    	S = S + C_j);
	    S
	    )
	);
    --Finds sign vectors by applying the sign function to
    --M * an interior vector
    signVectors = apply(interiorVectors, v -> sgn(M * v));
    signVectors = unique(signVectors);
    signVectors
    )

