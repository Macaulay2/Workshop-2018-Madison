
----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
------------- Example 2.2
----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
restart
needsPackage "VirtualResolutions";
X = toricProjectiveSpace(1)**toricProjectiveSpace(1);
S = ring X; B = ideal X;
J = saturate(intersect(
    ideal(x_1 - 1*x_0, x_3 - 4*x_2),
    ideal(x_1 - 2*x_0, x_3 - 5*x_2),
    ideal(x_1 - 3*x_0, x_3 - 6*x_2)),
     B);

minres = res J;
multigraded betti minres

multigradedRegularity(X, S^1/J)

vres = virtualOfPair(J,{{3,1}}) --(3,1) = (2,0) + (1,1)
multigraded betti vres

isVirtual(J,B,vres)

----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
------------- Example 2.4
----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
C = resolveViaFatPoint(J,B,{2,0})
isVirtual(J,B,C)

----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
------------- Example 3.1
----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
R = ZZ/101[z_0,z_1,z_2,z_3];
I = ideal(z_0*z_2-z_1^2, z_1*z_3-z_2^2, z_0*z_3-z_1*z_2);
J = curveFromP3toP1P2(I)

dim J

----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
------------- Example 3.2
----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
I = randomCurveP1P2(7,3);
S = ring I;
dim I

B = intersect(ideal(x_(0,0), x_(0,1)), ideal(x_(1,0), x_(1,1), x_(1,2)));
J = saturate(I,B);
multigradedRegularity(S, S^1/J)

minres = res J;
vres = virtualOfPair(S,minres,{{3,5}});

multigraded betti minres
multigraded betti vres

----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
------------- Remark 3.3
----------------------------------------------------------------------------------
----------------------------------------------------------------------------------

tally apply(500,i->(
	I := randomCurveP1P2(7,3,ZZ/32003);
	S := ring I;
	B := intersect(ideal(x_(0,0), x_(0,1)), ideal(x_(1,0), x_(1,1), x_(1,2)));
	J := saturate(I,B);
	multigradedRegularity(S, S^1/J)
	))

----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
------------- Example 3.4
----------------------------------------------------------------------------------
----------------------------------------------------------------------------------
I = randomRationalCurve(5,7);
dim I 
